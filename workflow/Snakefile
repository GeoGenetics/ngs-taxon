import numpy as np
snakemake.utils.min_version("8.11.2")

# Define wrapper versions
wrapper_ver = "v3.12.0"


include: "rules/utils.smk"
include: "rules/setup.smk"
include: "rules/map.smk"
include: "rules/mark_duplicates.smk"
include: "rules/merge_alns.smk"
include: "rules/bam_filter.smk"
include: "rules/metadmg.smk"



###########################
### Taxonomy assignment ###
###########################

rule multiqc:
    input:
        ### Bowtie2
        expand_pandas(expand_pandas(rules.bowtie2.log, bowtie_sets, allow_missing=True), units),
        ### MarkDuplicates
        expand_pandas(expand_pandas(get_rule_stats(config["align"]["mark_duplicates"]["tool"]), bowtie_sets, allow_missing=True), units) if is_activated("align/mark_duplicates") else [],
        ### Samtools Stats
        expand_pandas(expand_pandas(rules.samtools_stats.output.txt, bowtie_sets, allow_missing=True), units),
        ### metaDMG dfit
        expand_pandas(expand(rules.metadmg_dfit.output.dfit, read_type_map="collapsed", allow_missing=True), units),
        ### metaDMG LCA Aggregated Stats
        expand_pandas(expand(rules.metadmg_aggregate.output.stats, read_type_map="collapsed", allow_missing=True), units),
    output:
        html = "reports/multiqc.html",
        data = "reports/multiqc_data.zip",
    log:
        "logs/reports/multiqc.log"
    benchmark:
        "benchmarks/reports/multiqc.log"
    params:
        extra = "--force " + check_cmd(config["report"]["multiqc"], forbidden_args = ["-n", "--filename", "-o", "--outdir", "--data-dir", "--no-data-dir", "-z", "--zip-data-dir", "--no-report", "--pdf", "--version", "-h", "--help"]),
    localrule: True
    threads: 1
    resources:
        mem = lambda w, attempt: f"{10 * attempt} GiB",
        runtime = lambda w, attempt: f"{30 * attempt} m",
    wrapper:
        f"{wrapper_ver}/bio/multiqc"

rule all:
    input:
        qc = rules.multiqc.output.html,
        res = [expand_pandas(expand(rules.metadmg_damage.output.stats, read_type_map="collapsed", allow_missing=True), units),
               expand_pandas(expand(rules.metadmg_aggregate.output.stats, read_type_map="collapsed", allow_missing=True), units),
               expand_pandas(expand(rules.bam_filter_lca.output.stats, read_type_map="collapsed", allow_missing=True), units) if is_activated("bam_filter/lca") else [],
	       ],
    message:
        "Read mapping finished successfully!"
    default_target: True
